buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0'
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

import java.util.regex.Matcher
import java.util.regex.Pattern
import org.apache.tools.ant.taskdefs.condition.Os

android {
    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            java.srcDirs = [
                "./src",
                "./gen/java"
            ]
            jniLibs.srcDirs = [
                "./jniLibs/NDK",
                "./jniLibs/Qt"
            ]
            jni.srcDirs = []
            assets.srcDirs = ["./assets"]
        }
    }

    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 21
    }

    // Seems this has no effect at all, since not propagated. So should be inserted in host application
    aaptOptions {
        // Don't compress any resources
        noCompress "qz"
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            buildConfigField "boolean", "USE_DEBUG_LIBRARIES", "true"
        }

        release {
            debuggable false
            jniDebuggable false
            buildConfigField "boolean", "USE_DEBUG_LIBRARIES", "false"
        }
    }
}

// Java sources generation tasks
task cleanupSwig(type: Delete) {
    description "Clean-up old generated SWIG Java interface"

    delete "./gen"
}

task swigGenerateJava(type: Exec) {
    description "Generate SWIG Java interface"

    dependsOn cleanupSwig

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine "cmd", "/c", "bash --login " + file("../java/generate.sh").getAbsolutePath() + " " + projectDir.getAbsolutePath()
    } else {
        commandLine "sh", file("../java/generate.sh").getAbsolutePath(), projectDir.getAbsolutePath()
    }

    doLast {
        if (execResult.exitValue != 0) {
            throw new GradleException()
        }
    }
}
tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn << swigGenerateJava
}

// NDK libraries tasks
task cleanupNdkSharedLibs(type: Delete) {
    description "Clean-up NDK shared libraries"

    delete "./jniLibs/NDK"
}

task copyNdkSharedLibs(type: Copy) {
    description "Copy NDK shared libraries"

    dependsOn cleanupNdkSharedLibs

    from("$System.env.ANDROID_NDK/sources/cxx-stl/gnu-libstdc++/4.9/libs") {
        include "armeabi/libgnustl_shared.so"
        include "armeabi-v7a/libgnustl_shared.so"
        include "mips/libgnustl_shared.so"
        include "x86/libgnustl_shared.so"
    }

    into "./jniLibs/NDK"
    includeEmptyDirs = false
}
tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn << copyNdkSharedLibs
}

// Qt shared libraries tasks
task cleanupQtSharedLibs(type: Delete) {
    description "Clean-up Qt shared libraries"

    delete "./jniLibs/Qt"
}

task copyQtSharedLibs(type: Copy) {
    description "Copy Qt shared libraries"

    dependsOn cleanupQtSharedLibs

    from("../../externals/qtbase-android") {
        include "upstream.patched.android.gcc-*.shared/lib/libQt5Core.so"
        include "upstream.patched.android.gcc-*.shared/lib/libQt5Network.so"
        include "upstream.patched.android.gcc-*.shared/lib/libQt5Sql.so"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = fileCopyDetails.path.replaceAll(/.*upstream\.patched\.android\.gcc\-(.*)\.shared\/lib\/(.*)/, '$1/$2')
        }
    }

    into "./jniLibs/Qt"
    includeEmptyDirs = false
}
tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn << copyQtSharedLibs
}

// Qt JAR libraries tasks
task cleanupQtJarLibs(type: Delete) {
    description "Clean-up Qt JAR libraries"

    delete "./libs/Qt"
}

task copyQtJarLibs(type: Copy) {
    description "Copy Qt JAR libraries"

    dependsOn cleanupQtJarLibs

    from("../../externals/qtbase-android") {
        include "upstream.patched.android.gcc-*.shared/jar/*-bundled.jar"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = fileCopyDetails.path.replaceAll(/.*upstream\.patched\.android\.gcc\-.*\.shared\/jar\/(.*)/, '$1')
        }
    }

    into "./libs/Qt"
    includeEmptyDirs = false
}
tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn << copyQtJarLibs
}

// OsmAnd resources tasks
task cleanupOsmAndResources(type: Delete) {
    description "Clean-up OsmAnd resources"

    delete "./assets/OsmAndCore_ResourcesBundle"
}

task copyOsmAndResources(type: Copy) {
    description "Copy OsmAnd resources"

    dependsOn cleanupOsmAndResources

    // Map styles and related:
    from("../../../resources/rendering_styles") {
        include "default.render.xml"
        include "default.map_styles_presets.xml"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = "map/styles/" + fileCopyDetails.path
        }
    }

    // Map icons (Android mdpi == 1.0 ddf):
    from("../../../resources/rendering_styles/style-icons/drawable-mdpi") {
        include "h_*shield*.png"
        include "h_*osmc*.png"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = "[ddf=1.0]/map/shields/" + fileCopyDetails.path.replaceAll(/h_((.*shield.*)|(.*osmc.*))\.png/, '$1.png')
        }
    }
    from("../../../resources/rendering_styles/style-icons/drawable-mdpi") {
        include "h_*.png"
        exclude "h_*shield*.png"
        exclude "h_*osmc*.png"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = "[ddf=1.0]/map/shaders/" + fileCopyDetails.path.replaceAll(/h_(.*)\.png/, '$1.png')
        }
    }
    from("../../../resources/rendering_styles/style-icons/drawable-mdpi") {
        include "mm_*.png"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = "[ddf=1.0]/map/icons/" + fileCopyDetails.path.replaceAll(/mm_(.*)\.png/, '$1.png')
        }
    }

    // Map icons (Android hdpi == 1.5 ddf):
    from("../../../resources/rendering_styles/style-icons/drawable-hdpi") {
        include "h_*shield*.png"
        include "h_*osmc*.png"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = "[ddf=1.5]/map/shields/" + fileCopyDetails.path.replaceAll(/h_((.*shield.*)|(.*osmc.*))\.png/, '$1.png')
        }
    }
    from("../../../resources/rendering_styles/style-icons/drawable-hdpi") {
        include "h_*.png"
        exclude "h_*shield*.png"
        exclude "h_*osmc*.png"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = "[ddf=1.5]/map/shaders/" + fileCopyDetails.path.replaceAll(/h_(.*)\.png/, '$1.png')
        }
    }
    from("../../../resources/rendering_styles/style-icons/drawable-hdpi") {
        include "mm_*.png"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = "[ddf=1.5]/map/icons/" + fileCopyDetails.path.replaceAll(/mm_(.*)\.png/, '$1.png')
        }
    }

    // Map icons (Android xhdpi == 2.0 ddf):
    from("../../../resources/rendering_styles/style-icons/drawable-xhdpi") {
        include "h_*shield*.png"
        include "h_*osmc*.png"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = "[ddf=2.0]/map/shields/" + fileCopyDetails.path.replaceAll(/h_((.*shield.*)|(.*osmc.*))\.png/, '$1.png')
        }
    }
    from("../../../resources/rendering_styles/style-icons/drawable-xhdpi") {
        include "h_*.png"
        exclude "h_*shield*.png"
        exclude "h_*osmc*.png"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = "[ddf=2.0]/map/shaders/" + fileCopyDetails.path.replaceAll(/h_(.*)\.png/, '$1.png')
        }
    }
    from("../../../resources/rendering_styles/style-icons/drawable-xhdpi") {
        include "mm_*.png"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = "[ddf=2.0]/map/icons/" + fileCopyDetails.path.replaceAll(/mm_(.*)\.png/, '$1.png')
        }
    }

    // Map icons (Android xxhdpi == 3.0 ddf):
    from("../../../resources/rendering_styles/style-icons/drawable-xxhdpi") {
        include "h_*shield*.png"
        include "h_*osmc*.png"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = "[ddf=3.0]/map/shields/" + fileCopyDetails.path.replaceAll(/h_((.*shield.*)|(.*osmc.*))\.png/, '$1.png')
        }
    }
    from("../../../resources/rendering_styles/style-icons/drawable-xxhdpi") {
        include "h_*.png"
        exclude "h_*shield*.png"
        exclude "h_*osmc*.png"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = "[ddf=3.0]/map/shaders/" + fileCopyDetails.path.replaceAll(/h_(.*)\.png/, '$1.png')
        }
    }
    from("../../../resources/rendering_styles/style-icons/drawable-xxhdpi") {
        include "mm_*.png"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = "[ddf=3.0]/map/icons/" + fileCopyDetails.path.replaceAll(/mm_(.*)\.png/, '$1.png')
        }
    }

    // Misc map resources:
    from("../../../resources/rendering_styles/stubs/[ddf=1.0]") {
        include "*.png"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = "[ddf=1.0]/map/stubs/" + fileCopyDetails.path
        }
    }
    from("../../../resources/rendering_styles/stubs/[ddf=1.5]") {
        include "*.png"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = "[ddf=1.5]/map/stubs/" + fileCopyDetails.path
        }
    }
    from("../../../resources/rendering_styles/stubs/[ddf=2.0]") {
        include "*.png"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = "[ddf=2.0]/map/stubs/" + fileCopyDetails.path
        }
    }
    from("../../../resources/rendering_styles/stubs/[ddf=3.0]") {
        include "*.png"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = "[ddf=3.0]/map/stubs/" + fileCopyDetails.path
        }
    }
    
    from("../../../resources/rendering_styles/stubs") {
        include "*.png"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = "map/stubs/" + fileCopyDetails.path
        }
    }

    // Routing:
    from("../../../resources/routing") {
        include "routing.xml"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = "routing/" + fileCopyDetails.path
        }
    }

    // Fonts:
    from("../../../resources/rendering_styles/fonts") {
        include "**/*.ttf"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = "map/fonts/" + fileCopyDetails.path
        }
    }

    // Misc resources
    from("../../../resources/misc/icu4c") {
        include "*.dat"
        eachFile { fileCopyDetails ->
            fileCopyDetails.path = "misc/icu4c/" + fileCopyDetails.path.replaceAll(/icudt\d+([lb])\.dat/, 'icu-data-$1.dat')
        }
    }

    into "./assets/OsmAndCore_ResourcesBundle"
    includeEmptyDirs = false
}

task packOsmAndResources(type: Exec) {
    description "Pack OsmAnd resources"

    dependsOn copyOsmAndResources

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine "cmd", "/c", "bash --login " + file("./pack-resources.sh").getAbsolutePath() + " " + projectDir.getAbsolutePath() + "/assets/OsmAndCore_ResourcesBundle"
    } else {
        commandLine "sh", file("./pack-resources.sh").getAbsolutePath(), projectDir.getAbsolutePath() + "/assets/OsmAndCore_ResourcesBundle"
    }
}
tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn << packOsmAndResources
}

// OsmAnd resources indexing task
task indexOsmAndResources << {
    description = "Index OsmAnd resources"

    def resources = fileTree("./assets/OsmAndCore_ResourcesBundle") {
        include "**/*.*"
    }.collect {
        relativePath(it).replace('\\', '/').replaceAll("assets/OsmAndCore_ResourcesBundle/", "")
    }

    def indexFile = new File("./assets/OsmAndCore_ResourcesBundle.index")
    indexFile.text = resources.join('\n')
}
indexOsmAndResources.dependsOn << copyOsmAndResources
tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn << indexOsmAndResources
}

// Legacy OsmAnd for Android builds requires classes as a JAR, so add it as an artifact
artifacts {
    archives(file("build/intermediates/bundles/release/classes.jar")) {
        name "OsmAndCore_android"
        type "jar"
    }
}

// Uploading artifacts to local path
group = "net.osmand"
uploadArchives {
    repositories.ivy {
        credentials {
            username ""
            password ""
        }
        url = System.getenv("OSMAND_BINARIES_IVY_ROOT") ?: "./"
        layout "pattern" , {
            artifact "[organisation]/[module]/[revision]/[artifact]-[revision].[ext]"
        }

        def gitInfo = "git describe --long".execute().text.trim()
        def parsedGitInfo = Pattern.compile("v(\\d+\\.\\d+)-([\\d.]+)-(\\w+)").matcher(gitInfo)
        assert parsedGitInfo.matches()
        version = parsedGitInfo.group(1) + "-SNAPSHOT"
        //version = parsedGitInfo.group(1) + "." + parsedGitInfo.group(2)
    }
}

dependencies {
    compile fileTree(dir: "libs", include: ["**/*.jar"])
    releaseCompile project(":OsmAndCore_androidNativeRelease")
    debugCompile project(":OsmAndCore_androidNativeDebug")
}
